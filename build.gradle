buildscript {

    // Define the versions of the library and its dependencies.
    // As long as `buildscript` section is always evaluated first, we need to apply
    // `ext.gradle` explicitly here.
    apply from: "ext.gradle"

    ext {
        googleMavenCentralMirror = 'https://maven-central.storage.googleapis.com'
        spineRepository = 'http://maven.teamdev.com/repository/spine'
        spineSnapshotsRepository = 'http://maven.teamdev.com/repository/spine-snapshots'
    }

    repositories {
        jcenter()
        maven { url = googleMavenCentralMirror }

        mavenCentral()
        mavenLocal()

        // Spine releases repository.
        maven { url = spineRepository }

        // Spine snapshots repository.
        maven { url = spineSnapshotsRepository }
    }

    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVerison}"
    }
}

apply from: 'ext.gradle'

ext {
    protobufDependency = "com.google.protobuf:protoc:${project.protobufVersion}"
    publishPlugin = "$rootDir/script/publish.gradle"

    credentialsPropertyFile = 'credentials.properties'
    runsOnWindows = org.gradle.internal.os.OperatingSystem.current().isWindows()

    projectsToPublish = ['datastore']
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    group = 'io.spine'
    version = project.spineVersion

    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
        maven { url = googleMavenCentralMirror }

        mavenCentral()
        mavenLocal()

        // Spine releases repository.
        maven { url = spineRepository }

        // Spine snapshots repository.
        maven { url = spineSnapshotsRepository }
    }

    dependencies {
        // Exclude the transient dependencies from Spine, that aren't needed for GAE implementation.
        final def excludes = {
            exclude group: 'com.google.protobuf', module: 'protobuf-lite'
            exclude group: 'io.grpc'
        }

        compile group: 'io.spine', name: "spine-server",
                version: spineVersion, changing: true, excludes


        // Guava
        compile group: 'com.google.guava', name: 'guava', version: guavaVersion

        // Protobuf runtime
        compile group: 'com.google.protobuf', name: 'protobuf-java', version: protobufVersion
        compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: protobufVersion

        // Findbugs
        compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'

        // SLF4J
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
        compile group: 'org.slf4j', name: 'slf4j-jdk14', version: slf4jVersion

        testCompile group: 'io.spine', name: "spine-testutil-server", version: spineVersion,
                changing: true, excludes
        testCompile group: 'io.spine', name: "spine-server", version: spineVersion,
                changing: true, classifier: "test", excludes

        // JUnit
        testCompile(group: 'junit', name: 'junit', version: '4.12') {
            exclude(module: 'hamcrest-core')
        }
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.+'
        testCompile group: 'com.google.guava', name: 'guava-testlib', version: guavaVersion
    }

    protobuf {
        generatedFilesBaseDir = "$projectDir/generated"
        protoc {
            artifact = project.protobufDependency
        }
    }

    javadoc {
        source = sourceSets.main.allJava
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        classifier "test"
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from("$projectDir/build/docs/javadoc")
        classifier "javadoc"
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination = ("${buildDir}/reports/jacoco/report.xml" as File)
        html.enabled = false
        csv.enabled = false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

task startDatastore {
    description "Starts local in-memory datastore using scripts form ./script folder."
    group "Build Setup"
}

startDatastore.doLast {
    // Start Datastore emulator with the CLI command
    ".${File.separatorChar}script${File.separatorChar}start-datastore.${runsOnWindows ? 'bat' : 'sh'}".execute()
}

tasks["check"].dependsOn startDatastore

apply from: publishPlugin
