/*
 * Copyright 2018, TeamDev Ltd. All rights reserved.
 *
 * Redistribution and use in source and/or binary forms, with or without
 * modification, must retain the above copyright notice and the following
 * disclaimer.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
 * "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
 * LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
 * A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
 * LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
 * DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
 * THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

apply from: "$rootDir/scripts/js-cli.gradle"

ext {
    JAVA_SCRIPT_TASK_GROUP = 'JavaScript'

    workDirectory = "$buildDir/node-module/"
    publicationDirectory = "$buildDir/npm-publication/"

    assemblyComponents = ['package.json', 'proto', 'src', 'test', '.babelrc']
    publicationComponents = ['package.json', 'proto/main/js', 'src']

    npm = { final String... command ->
        ext.executeNpm(workDirectory as File, command)
    }
}

task compileProtoToJs {}

protobuf {
    generatedFilesBaseDir = "$projectDir/proto"
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generateProtoTasks {
        all().each { final task ->
            task.builtins {
                // Do not use java builtin output in this project.
                remove java

                // For information on JavaScript code generation please see
                // https://github.com/google/protobuf/blob/master/js/README.md
                js {
                    option "import_style=commonjs"
                }
            }
            compileProtoToJs.dependsOn task
        }
    }
}

String filterPackageDescriptor(final String line) {
    if (line.matches('.*private.*')) {
        return null
    } else if (line.contains('$PACKAGE_VERSION')) {
        return line.replaceAll('\\$PACKAGE_VERSION', spineVersion)
    } else if (line.contains('$PACKAGE_TEST')) {
        return line.replaceAll('\\$PACKAGE_TEST', 'mocha --require babel-polyfill --require babel-register --recursive \\$NODE_DEBUG_OPTION --exit --full-trace ./test')
    } else {
        return line
    }
}

task copyModuleSources(type: Copy) {
    from fileTree(projectDir).include {
        final def path = it.path
        for (final def branch in assemblyComponents) {
            if (path.startsWith(branch)) {
                return true
            }
        }
        return false
    }
    into workDirectory

    filter { filterPackageDescriptor(it) }

    dependsOn compileProtoToJs
}

/**
 * Installs the module dependencies using the `npm install` command.
 */
task installDependencies {
    group = JAVA_SCRIPT_TASK_GROUP
    description = 'Installs the JavaScript dependencies.'

    inputs.files "$projectDir/package.json"
    outputs.files "$projectDir/node_modules"

    dependsOn copyModuleSources

    doLast {
        npm 'install'
    }

    outputs.upToDateWhen { false }
}

/**
 * Assembles the JS sources.
 *
 * This task is an analog of `build` for JS.
 *
 * To include a task into the JS build, depend `buildJs` onto that task.
 */
task buildJs {
    group = JAVA_SCRIPT_TASK_GROUP
    description = "Assembles the JavaScript source files."

    dependsOn installDependencies, compileProtoToJs
}

/**
 * Copies the NPM publication files into a temporary directory which they are published from.
 */
task prepareJsPublication(type: Copy) {
    group = JAVA_SCRIPT_TASK_GROUP
    description = 'Prepares the NPM package for publish.'

    from publicationComponents.collect {
        final def file = "$projectDir/$it" as File
        if (!file.exists()) {
            return null
        } else {
            return file
        }
    }.findAll { it != null }
    into publicationDirectory
    exclude "test"

    filter { filterPackageDescriptor(it) }

    dependsOn buildJs
}

/**
 * Publishes the NPM package locally with `npm link`.
 */
task link {
    group = JAVA_SCRIPT_TASK_GROUP
    description = "Publishes the NPM package locally."

    doLast {
        executeNpm(publicationDirectory as File, 'link')
    }

    dependsOn prepareJsPublication
}

/**
 * Publishes the NPM package with `npm publish`.
 */
task publishJs {
    group = JAVA_SCRIPT_TASK_GROUP
    description = 'Publishes the NPM package.'

    doLast {
        executeNpm(publicationDirectory as File, 'publish')
    }

    dependsOn prepareJsPublication
}

task testJs {
    group = JAVA_SCRIPT_TASK_GROUP
    description = 'Runs the JS tests.'

    doLast {
        npm 'run', 'test'
    }

    dependsOn buildJs
}

task coverageJs {
    group = JAVA_SCRIPT_TASK_GROUP
    description = 'Runs the JS test and collects the code coverage info.'

    doLast {
        npm 'run', 'test'
    }

    dependsOn buildJs
}

rootProject.codeCoverageReport.dependsOn coverageJs

build.dependsOn buildJs
publish.dependsOn publishJs
